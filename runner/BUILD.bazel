load("@rules_python//python:defs.bzl", "py_binary", "py_library")
load("@pypi//:requirements.bzl", "requirement")
load("//rules/build:exec_binary.bzl", "exec_binary")

py_library(
    name = "lib",
    srcs = glob(["src/runner/*.py"]),
    imports = ["src"],
    deps = [
        # https://github.com/bazel-contrib/rules_python/tree/main/python/runfiles
        requirement("bazel-runfiles"),  # also "@rules_python//python/runfiles" can be used (but need import python.runfiles instead of import runfiles)
        requirement("colorama"),  # also "@pypi//colorama" can be used
        requirement("filetype"),
    ],
    visibility = ["//visibility:public"],
)

py_binary(
    name = "bin",
    srcs = ["src/main.py"],
    main = "src/main.py",
    tags = ["manual"],
    deps = [":lib"],
    visibility = ["//visibility:private"],
)

# Wrapper that forces runner to be built for exec platform
# It fixes usage of runner on Windows when it's used as a tool (--run_under for WASM tests),
#   because otherwise it would be built for target platform (without .exe) and fails to run.
exec_binary(
    name = "runner",
    binary = ":bin",
    visibility = ["//visibility:public"],
)

# Wrapper allowing to run different executable targets on execution platform.
# 1. Executable that have multiple outputs (for example py_binary on Windows w/ .exe & script)
#    cannot be used as an input of sh_binary / sh_test to pass argument.
# 2. It also allows to run stored arguments in a file (for example, generated by genrule) 
#    to allow composition of multiple executables in a single sh_test (for example, server + client + bridge to run them).
exports_files(["sh_wrapper.cmd"])
